\print {
	/* Output results of read-back. */
	this.nf = RVAL;
	++this.total;
} \atom;

\read[a] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(b, \read_{LVAL}(a)), b];

\read[a] {
	/* Initiate application. */
	++this.total;
} \apply[\lambda(b, \read_{LVAL}(a)), b];

\read[a] {
	/* Read back abstraction. */
	++this.total;
} \lambda[\atom_{this.mkvar(true)}, \read_{this.abst(LVAL, this.mkvar())}(a)];

\lambda[\read_{this.appl("%s", RVAL)}(a), a] {
	/* Read back application. */
	++this.total;
} \atom;

\read[\atom_{this.atom(LVAL, RVAL)}] {
	/* Read back an atom. */
	++this.total;
} \atom;

\copy[\atom_{RVAL}, \atom_{RVAL}] {
	/* Copy an atom. */
	++this.total;
} \atom;

\dup[\atom_{RVAL}, \atom_{RVAL}] {
	/* Duplicate an atom. */
	++this.total;
} \atom;

\lambda[a, b] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(c, \lambda(a, b)), c];

\lambda[a, b] {
	/* Initiate application. */
	++this.total;
} \apply[\lambda(c, \lambda(a, b)), c];

\lambda[a, b] {
	/* Apply a closed term. */
	++this.beta;
	++this.total;
} \lambda[a, b];

\copy[a, b] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(c, \copy(a, b)), c];

\copy[a, b] {
	/* Initiate application. */
	++this.total;
} \apply[\lambda(c, \copy(a, b)), c];

\copy[\lambda(a, b), \lambda(c, d)] {
	/* Initiate copy of a closed term. */
	++this.total;
} \lambda[\dup(a, c), \dup(b, d)];

\dup[a, b] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(c, \dup(a, b)), c];

\dup[a, b] {
	/* Duplicate sharing. */
	++this.total;
} \copy[\dup(\amb(c, \share(a, d), d), \amb(e, \share(b, f), f)), \dup(c, e)];

\dup[\apply(a, b), \apply(c, d)] {
	/* Duplicate application. */
	++this.total;
} \apply[\dup(a, c), \dup(b, d)];

\dup[\lambda(a, b), \lambda(c, d)] {
	/* Duplicate abstraction. */
	++this.total;
} \lambda[\dup(a, c), \dup(b, d)];

\dup[a, b] {
	/* Finish duplication. */
	++this.total;
} \dup[a, b];

\erase {
	/* Erase an atom. */
	++this.total;
} \atom;

\erase {
	/* Erase sharing. */
	++this.total;
} \share[a, a];

\erase {
	/* Erase application. */
	++this.total;
} \apply[\erase, \erase];

\erase {
	/* Erase abstraction. */
	++this.total;
} \lambda[\erase, \erase];

\erase {
	/* Erase copy initiator. */
	++this.total;
} \copy[\erase, \erase];

\erase {
	/* Erase duplicator. */
	++this.total;
} \dup[\erase, \erase];

\erase {
	/* Finish erasing. */
	++this.total;
} \erase;

$$

INCONFIG

$$

var id = 0;

function mkvar(fresh)
{
	if (fresh)
		++id;

	return "v" + id.toFixed(0);
}

function place(buf, format, str)
{
	var sub = format.replace("%s", str);

	sub = sub.replace("%%", "%s");

	return buf.replace("%s", sub);
}

function abst(buf, str)
{
	return place(buf, "%s: %%", str);
}

function appl(buf, str)
{
	return place(buf, "%s (%%)", str);
}

function atom(buf, str)
{
	return place(buf, "%s", str)
}

this.mkvar = mkvar;
this.abst = abst;
this.appl = appl;
this.atom = atom;
this.beta = 0;
this.total = 0;
